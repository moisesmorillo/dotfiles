#!/usr/bin/env zsh

autoload -U colors && colors # enable $fg, $reset_color
# shellcheck disable=SC1090

# This function is used to test the speed of zsh startup
zsh_time() {
	repeat 10 time zsh -i -c exit
}

# This function is used to update the OS packages, dotfiles and LazyVim core and plugins
fu() {
	OS=$(uname -s)

	echo "*** Updating OS packages***"

	if [[ "$OS" == "Darwin" ]]; then
		brew update
		brew upgrade
	fi

	if [[ "$OS" == "Linux" ]]; then
		sudo apt update -y
		sudo apt full-upgrade -y
		sudo apt autoremove -y
		sudo apt autoclean -y
	fi

	echo "*** Updating dotfiles ***"
	cd ~/workspace/dotfiles/ && git pull origin main && cd - || exit

	echo "*** Updating LazyVim core and plugins ***"
	nvim --headless "+Lazy! sync" +qa

	echo "*** Updating zinit and plugins ***"
	zinit self-update
	zinit update --parallel
	stow_dotfiles
	zsh_reload
}

# This function is used to reload the zsh configuration
zsh_reload() {
	echo "*** Reloading zsh configuration ***"
	source ~/.zshrc
	exec zsh
}

# This function is used to re-stow the dotfiles
stow_dotfiles() {
	current_dir=$(pwd)
	echo "*** Restowing dotfiles ***"
	cd ~/workspace/moisesmorillo/dotfiles/ && stow --ignore="network_utils" -R */
	cd "$current_dir" || return
}

# This function is used to kill the podman process (docker)
kdo() {
	pgrep -fi docker | xargs kill
}

# This function is used to reinstall the LazyVim dotfiles
reinstall_lazyvim() {
	current_dir=$(pwd)
	echo "*** Unstowing Neovim directory ***"
	cd ~/workspace/dotfiles/ || return
	stow -v -D nvim
	echo "*** Removing Neovim and LazyVim directories ***"
	rm -rf ~/.config/nvim/ ~/.local/share/nvim/ ~/.local/state/nvim/ ~/.cache/nvim/
	echo "*** Restowing Neovim directory ***"
	stow -v -R nvim
	cd "$current_dir" || return
	echo "*** Reinstalling LazyVim plugins ***"
	nvim --headless "+Lazy! sync" +qa
}

# This function is used to install the LazyVim test environment
install_nvim_distro() {
	distro=$1

	case $distro in
	"nvchad")
		echo "Installing NvChad..."
		git clone --depth 1 https://github.com/NvChad/starter "$HOME/.config/$distro"
		;;
	"lazytest")
		echo "Installing LazyVim Test..."
		git clone --depth 1 https://github.com/LazyVim/starter "$HOME/.config/$distro"
		;;
	"kickstart")
		echo "Installing Kickstart..."
		git clone --depth 1 https://github.com/nvim-lua/kickstart.nvim "$HOME/.config/$distro"
		;;
	"cybernvim")
		echo "Installing CyberNvim..."
		git clone --depth 1 https://github.com/pgosar/CyberNvim "$HOME/.config/$distro"
		;;
	"astronvim")
		echo "Installing AstroNvim..."
		git clone --depth 1 https://github.com/AstroNvim/template "$HOME/.config/$distro"
		;;
	*)
		echo "Invalid distro"
		;;
	esac

	rm -rf "$HOME/.config/$distro/.git"
}

# This function is used to clean the LazyVim test environment
clean_nvim_distro() {
	distro=$1
	echo "Cleaning $distro..."
	rm -rf "$HOME/.config/$distro"
	rm -rf "$HOME/.local/share/$distro"
	rm -rf "$HOME/.local/state/$distro"
	rm -rf "$HOME/.cache/$distro"
}

# This function is used to rebuild the zsh completions
rebuild_zsh_completions() {
	echo "Rebuilding zcompdump"
	rm -f ~/.zcompdump
	compinit
}

# This function is used to create python virtual environment
venv_create() {
	python_version=$1
	if [[ -n $python_version ]]; then
		uv venv -p "$python_version"
	else
		uv venv
	fi
}

# This function is used to activate python virtual environment
venv_activate() {
	# shellcheck disable=SC1091
	source .venv/bin/activate
}

# This function is used to install dependencies using poetry
poetry_install_dependencies() {
	pip install -U pip
	pip install poetry
	poetry config virtualenvs.create false
	poetry install -E dev -E test --no-interaction
}

# This function is used to switch between nvim distros
switch_nvim_distro() {
	declare -A config_map=(
		["LazyVim Test"]="lazytest"
		["NvChad"]="nvchad"
		["AstroNvim"]="astronvim"
		["CyberNvim"]="cybernvim"
		["Kickstart"]="kickstart"
	)

	items=("LazyVim Test" "NvChad" "AstroNvim" "CyberNvim" "Kickstart")
	selected=$(printf "%s\n" "${items[@]}" | fzf --prompt="Óò´ Neovim Config: " --height=~50% --layout=reverse --border --exit-0)

	if [[ -z $selected ]]; then
		echo "Nothing selected"
		return 0
	fi

	config=${config_map[$selected]} # Obtener el valor mapeado
	NVIM_APPNAME=$config nvim "$@"
}

# This function is used to sync the plugins in the LazyVim distro with the dotfiles repo
sync_lazyvim_plugins() {
	cd ~/workspace/dotfiles || return

	git checkout -B chore/sync
	git commit -am "chore: sync LazyVim plugins"
	git push -u origin chore/sync
	gh pr create --fill --base main --head chore/sync
	gh pr merge --merge chore/sync
	git checkout main
	git branch -D chore/sync
	git pull origin main
	git fetch --prune
}
# This function is used to generate/regenerate the ~/Brewfile file
brewfile_generate() {
	brew bundle dump --force --file=~/Brewfile
}

# This function is used to install brew system from ~/Brewfile file
brewfile_install() {
	brew bundle install --file=~/Brewfile
}

# This function is used to force install brew system from ~/Brewfile file
brewfile_force_install() {
	brew bundle --force cleanup --file=~/Brewfile
}

# Clone a repo into ~/workspace/{owner}/{repo} (all lowercase) and cd into it.
# Usage:  gitclone <url> [branch]
# Example: gitclone git@github.com:owner/repo.git main
gitclone () {
  # Local options: pipefail + noclobber only inside the function
  emulate -L zsh -o pipefail -o noclobber

  local repo_url=$1
  local branch=$2

  # ---------------- Validate arguments ----------------------------------
  if [[ -z $repo_url ]]; then
    print -P "%F{red}‚ùå  Repository URL required%f"
    print   "Usage: gitclone <url> [branch]"
    return 1
  fi

  # ---------------- Extract owner/repo (POSIX-safe regex) --------------
  local owner repo
  local re='^(git@|https://)([^/:]+)[/:]([^/]+)/([^/]+)(\.git)?$'

  if [[ $repo_url =~ $re ]]; then
    owner=${match[3]:l}            # lowercase
    repo=${match[4]:l}
    repo=${repo%.git}              # strip trailing .git, if any
  else
    print -P "%F{red}‚ùå  Unsupported URL format%f"
    return 1
  fi

  # ---------------- Destination path ------------------------------------
  local workspace_root=${GITCLONE_WORKSPACE:-$HOME/workspace}
  local target_dir="$workspace_root/$owner/$repo"

  # ---------------- Clone or reuse --------------------------------------
  if [[ -d $target_dir/.git ]]; then
    print -P "%F{yellow}‚ö†Ô∏è  Repository already exists at%f $target_dir"
  else
    mkdir -p "$target_dir" || {
      print -P "%F{red}‚ùå  Cannot create%f $target_dir"
      return 1
    }

    print -P "%F{green}üîÑ  Cloning into%f $target_dir"
    if [[ -n $branch ]]; then
      git clone --branch "$branch" --single-branch "$repo_url" "$target_dir" \
        || { print -P "%F{red}‚ùå  Clone failed%f"; return 1 }
    else
      git clone  "$repo_url" "$target_dir" \
        || { print -P "%F{red}‚ùå  Clone failed%f"; return 1 }
    fi
  fi

  # ---------------- Enter the directory ---------------------------------
  cd "$target_dir"
}
